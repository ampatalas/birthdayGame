*if warrior
	*if leave_lea
		*goto_scene kale_grave

	*else
		*goto_scene lea_cave

*if rogue
	*if with_alex
		*goto_scene alex_story
	
	*else
		*goto_scene kale_grave

*if mage
	*if guard_help
		The next day in the tavern, you try to make up a plan for what happens next. The village is small, and it won't bring you as much income as big city would. Still, you need some money and some place to stay. In the times when the king is sick, people aren't very trustworthy of witches. The people in Lake Town seem friendly enough.

		*page_break

		For next few days, you perform for them, until their pockets go dry. You start thinking about leaving town, but then the little girl comes to you again.

		"Can you show me a magic trick?" She asks.

		"You sure like magic tricks," you say. This time, you don't think twice about showing her your powers. She's quite odd for a child, and others have seen you performing anyway.

		She claps. "I should do something for you in return," she says.

		*choice
			#"Well, do you have a coin to spare?"
				She smirks, and there's something adult and weird about that smirk of hers.
				
				"No," she says, "But I have better things."
				
				"Let me guess," you say. "Love or friendship."
				
				*goto gift

			#"It's nothing, child. Consider the show a gift."
				*label gift
				Kid shakes her head and hands you a necklace. You gasp - it's gold and looks expensive.

				"Your parents are going to be very mad with you if you give away your stuff like that," you say.

				*page_break

				The child giggles. "That's not the case," she explains. "The value of the necklace is not in its gold. In fact, it's not even real gold, just an illusion. It's not worth anything to a simple folk," her eyes bight up. "But it can be very helpful for a witch."

				*choice
					#You don't really want the necklace anymore.
						You try to give it back to her, but she insists. "Take it," she says. "I have no use of it anymore."
						
						*goto reveal

					#You are excited about it's magic. You take it.
						*label reveal
						"You're not a child," you state the obvious. "Are you a witch? Are you a demon?"

						She shakes her head. "I'm neither of those. But consider me your ally. For now."

						*page_break

						You leave the town. As you arrive from one spot to another, you notice a change. It feels almost like your powers are getting stronger. It's great, but it's scary too - you feel a new wave of feelings and temptations that you have never experienced before, almost as your powers were taking control over you.

						Few times, you consider getting rid of the wicked necklace, but you never actually decide to do it.

						*goto_scene necklace_tale

	*else
		*if tavern_entertainer
			Every evening, you visit the main room in the tavern and perform the magic arts, or, 'tricks', as the customers say. You focus on the visuals - you blend elements together in perfect harmony. The lights fill the room or keep it in the shadow. You're a director of your own show.

			Sometimes, when the audience is enthusiastic enough, you reward them. You start with little smells - some chocolate and vanilla filling the room, like world's best bakery. Sometimes, you add nice sensations running through their skin. And, before you know it, the rumours about you bewitching others start.

			*choice
				#Fair enough. You're a player anyway.
					You keep on enchatin'. The tavern stops being safe for you, but you have no worries - there's always the next bar to play in and the new heart to steal. Soon, you're known as the notorious seducer of the land, and every parent warns their kid from you.

					The bast part of it is, it's not even magic.
					*finish
				#You decide to leave before it gets too dangerous.
					As you gather your things, someone approaches you. He's a man, quite your age, and he says he had seen the show night before. He suggests escorting you to the next village.

					*page_break

					"You're not scared of my evil magic?" You ask, amused.

					He shakes his head. 

					"There's nothing evil about your magic." He says. "I've seen some mad, evil magic - and yours is nothing like this."

					You look him up and down. He seems helpful enough.

					"Come with me," you say.

					*page_break

					He joins you on your journey, and eventually never leaves.
					*finish
		*else
			*finish